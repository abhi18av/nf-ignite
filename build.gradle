/*
 * Copyright 2013-2019, Centre for Genomic Regulation (CRG)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'

version = file('VERSION').text.trim()
group = 'io.nextflow'

sourceCompatibility = 1.8
targetCompatibility = 1.8


sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main']
    main.resources.srcDirs = ['src/resources']
    // this is required due to this IDEA bug
    // https://youtrack.jetbrains.com/issue/IDEA-129282
    main.output.resourcesDir = 'build/classes/main'
    test.groovy.srcDirs = ['src/test']
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

idea {
    module.inheritOutputDirs = true
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.eclipse.org/content/groups/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-cli-picocli'
    // groovydoc libs
    groovyDoc.extendsFrom runtime
}

dependencies {
    compile 'io.nextflow:nextflow:20.08.0-edge'

    compile ('org.apache.ignite:ignite-core:2.4.0')
    compile ('org.apache.ignite:ignite-slf4j:2.4.0')

    testCompile ("org.codehaus.groovy:groovy-test:3.0.5") { exclude group: 'org.codehaus.groovy' }
    testCompile ("cglib:cglib-nodep:3.3.0")
    testCompile ("org.objenesis:objenesis:3.1")
    testCompile ("org.spockframework:spock-core:2.0-M3-groovy-3.0") { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }
    testCompile ('org.spockframework:spock-junit4:2.0-M3-groovy-3.0') { exclude group: 'org.codehaus.groovy'; exclude group: 'net.bytebuddy' }
    testCompile 'com.google.jimfs:jimfs:1.1'

    /* testCompile inherited from top gradle build file */
    testCompile ('org.apache.ignite:ignite-spring:2.4.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    modules {
        module("commons-logging:commons-logging") { replacedBy("org.slf4j:jcl-over-slf4j") }
    }

    // Documentation required libraries
    groovyDoc 'org.fusesource.jansi:jansi:1.11'
    groovyDoc "org.codehaus.groovy:groovy-groovydoc:3.0.5"
    groovyDoc "org.codehaus.groovy:groovy-ant:3.0.5"
}


application {
    mainClassName = 'nextflow.cli.Launcher'
}
